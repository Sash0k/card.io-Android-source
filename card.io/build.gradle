apply plugin: 'com.android.library'
apply plugin: 'signing'

repositories {
    mavenCentral()
    google()
}

android {
    compileSdkVersion rootProject.ext.sdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    namespace 'io.card.payment'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.sdkVersion

        ndk {
            moduleName "cardioRecognizer"
        }

        consumerProguardFiles 'proguard.cfg'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildFeatures {
            buildConfig = true
        }
        versionName = "5.5.1-OS1"
        buildTypes.configureEach {
            buildConfigField "String", "CARD_IO_SDK_VERSION", "\"$versionName\""
        }
    }

    resourcePrefix 'cio_'

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    ndkVersion rootProject.ext.ndkVersion
}

dependencies {
    implementation 'androidx.activity:activity:1.10.1'
    testImplementation "junit:junit:4.12"
    testImplementation "org.robolectric:robolectric:3.1.2"
}

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false

    options.docTitle("<h1>card.io Android SDK</h1>")
    options.bottom("<i>Copyright &#169; 2012 PayPal, Inc. All Rights Reserved.</i>")
    options.windowTitle("card.io Android SDK")
    options.author(true)
    options.version(true)
    options.use(true)
    options.setMemberLevel(JavadocMemberLevel.PUBLIC)

    // enables links to Java 7 docs
    options.links("http://docs.oracle.com/javase/7/docs/api/")
    // enables links to Android docs.
    options.linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference")
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    setArchiveClassifier('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    setArchiveClassifier('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}
